#!/usr/bin/env zsh

FONT_FACE="JetBrainsMono Nerd Font"
CONFIG_DIR="$HOME/Dotfiles/SketchyBar/.config/sketchybar"
PLUGIN_DIR="$CONFIG_DIR/plugins"
SPOTIFY_EVENT="com.spotify.client.PlaybackStateChanged"

source "$CONFIG_DIR/colorschemes.sh"
# COLOR_SCHEME_NAME="catppuccin-light"
COLOR_SCHEME_NAME="rose-pine"
load_colorscheme "$COLOR_SCHEME_NAME"

BAR_HEIGHT=30
ITEM_HEIGHT=26
CORNER_RADIUS=9
FRONT_ICON_WIDTH=32

MAIN_DISPLAY=$(system_profiler SPDisplaysDataType | grep -B 3 'Main Display:' | awk '/Display Type/ {print $3}')
SPOTIFY_LABEL_MAX=22

if [[ "$MAIN_DISPLAY" == "Built-in" ]]; then
  DEFAULT_ICON_FONT_SIZE="13.5"
  DEFAULT_LABEL_FONT_SIZE="13.0"
  SPACE_FONT_SIZE="12.5"
  FRONT_ICON_FONT_SIZE="15.0"
  FRONT_LABEL_FONT_SIZE="13.5"
  source "$CONFIG_DIR/sketchybarrc-laptop"
else
  DEFAULT_ICON_FONT_SIZE="14.5"
  DEFAULT_LABEL_FONT_SIZE="14.0"
  SPACE_FONT_SIZE="13.5"
  FRONT_ICON_FONT_SIZE="16.0"
  FRONT_LABEL_FONT_SIZE="14.25"
  SPOTIFY_LABEL_MAX=36
  source "$CONFIG_DIR/sketchybarrc-desktop"
fi
export SPOTIFY_LABEL_MAX

default=(
  padding_left=5
  padding_right=5
  icon.font="$FONT_FACE:Semibold:${DEFAULT_ICON_FONT_SIZE}"
  label.font="$FONT_FACE:Medium:${DEFAULT_LABEL_FONT_SIZE}"
  icon.color=$COLOR_TEXT
  label.color=$COLOR_TEXT
  label.y_offset=1
  icon.padding_left=4
  icon.padding_right=4
  label.padding_left=4
  label.padding_right=4
  background.corner_radius=$CORNER_RADIUS
  background.height=$ITEM_HEIGHT
  background.drawing=off
)

sketchybar --default "${default[@]}"

sketchybar --add event aerospace_workspace_change
sketchybar --add event spotify_change $SPOTIFY_EVENT

SPACE_PILL_WIDTH=34
LEFT_SPACE_PADDING=$((SPACE_PILL_WIDTH / 4))

space_item=(
  background.drawing=on
  background.corner_radius=$CORNER_RADIUS
  background.height=$ITEM_HEIGHT
  background.border_width=1
  background.border_color=$COLOR_BORDER
  background.padding_left=0
  background.padding_right=0
  width=$SPACE_PILL_WIDTH
  icon.drawing=off
  label.font="$FONT_FACE:Semibold:${SPACE_FONT_SIZE}"
  label.align=center
  label.width=$SPACE_PILL_WIDTH
  label.padding_left=0
  label.padding_right=0
  label.y_offset=0
)

typeset -a SPACES_LIST=()

if command -v aerospace >/dev/null 2>&1; then
  SPACES_LIST=("${(@f)$(aerospace list-workspaces --all 2>/dev/null)}")
fi

if [[ ${#SPACES_LIST[@]} -eq 0 ]]; then
  SPACES_LIST=(1 2 3 4 5)
fi

sketchybar --add item space.left_padding left \
           --set space.left_padding \
               width=$LEFT_SPACE_PADDING \
               background.drawing=off \
               icon.drawing=off \
               label.drawing=off

for sid in "${SPACES_LIST[@]}"; do
  sketchybar --add item space.$sid left \
             --set space.$sid "${space_item[@]}" \
                 label="$sid" \
                 background.color=$SPACE_INACTIVE_BG \
                 label.color=$SPACE_INACTIVE_FG \
                 click_script="aerospace workspace $sid" \
                 script="$PLUGIN_DIR/aerospace.sh $sid $SPACE_ACTIVE_BG $SPACE_INACTIVE_BG $SPACE_ACTIVE_FG $SPACE_INACTIVE_FG" \
             --subscribe space.$sid aerospace_workspace_change
done

sketchybar --add item front_app.gap left \
           --set front_app.gap \
               width=4 \
               padding_left=0 \
               padding_right=0 \
               background.drawing=off \
               icon.drawing=off \
               label.drawing=off

sketchybar --add item front_app left \
           --set front_app \
               script="$PLUGIN_DIR/front_app.sh" \
               icon.color=$COLOR_ACCENT_TEXT \
               icon.font="$FONT_FACE:Semibold:${FRONT_ICON_FONT_SIZE}" \
               icon.width=$FRONT_ICON_WIDTH \
               icon.align=center \
               icon.padding_left=0 \
               icon.padding_right=0 \
               icon.y_offset=0 \
               width=$FRONT_ICON_WIDTH \
               padding_left=0 \
               padding_right=0 \
               background.padding_left=6 \
               background.padding_right=6 \
               background.corner_radius=$CORNER_RADIUS \
               background.drawing=on \
               background.color=$COLOR_ACCENT \
               label.drawing=off

sketchybar --add item front_app.spacer left \
           --set front_app.spacer \
               width=10 \
               background.drawing=off \
               icon.drawing=off \
               label.drawing=off

sketchybar --add item front_app.name left \
           --set front_app.name \
               icon.drawing=off \
               label.font="$FONT_FACE:Semibold:${FRONT_LABEL_FONT_SIZE}" \
               label.color=$COLOR_TEXT \
               label.y_offset=0 \
               label.padding_left=12 \
               label.padding_right=14 \
               background.drawing=on \
               background.color=$COLOR_ITEM_ALT

sketchybar --add bracket front_app_block front_app front_app.name \
           --set front_app_block background.drawing=off \
           --subscribe front_app front_app_switched

right_module=(
  background.drawing=on
  background.corner_radius=$CORNER_RADIUS
  background.height=$ITEM_HEIGHT
  background.padding_left=12
  background.padding_right=12
  background.color=$COLOR_ITEM_BG
  background.border_width=1
  background.border_color=$COLOR_BORDER
  icon.color=$COLOR_DIM
  label.color=$COLOR_TEXT
)

add_right_gap() {
  local name="$1"
  sketchybar --add item "$name" right \
             --set "$name" width=6 icon.drawing=off label.drawing=off background.drawing=off
}

add_right_gap right.space.0

sketchybar --add item clock right \
           --set clock "${right_module[@]}" \
               icon=󰥔 \
               label="--:--" \
               update_freq=30 \
               script="$PLUGIN_DIR/clock.sh"

add_right_gap right.space.clock

sketchybar --add item volume right \
           --set volume "${right_module[@]}" \
               icon=󰕾 \
               label="--%" \
               script="$PLUGIN_DIR/volume_backup.sh" \
           --subscribe volume volume_change

add_right_gap right.space.2

sketchybar --add item battery right \
           --set battery "${right_module[@]}" \
               icon=󰁹 \
               label="--%" \
               update_freq=120 \
               script="$PLUGIN_DIR/battery.sh" \
           --subscribe battery system_woke power_source_change

add_right_gap right.space.3

sketchybar --add item spotify right \
           --set spotify "${right_module[@]}" \
               icon= \
               icon.color=$COLOR_ACCENT \
               label="Spotify Idle" \
               label.max_chars=$SPOTIFY_LABEL_MAX \
               background.color=$COLOR_ITEM_ALT \
               script="$PLUGIN_DIR/spotify_now.sh" \
               click_script="open -a 'Spotify'" \
           --subscribe spotify spotify_change

sketchybar --update
sketchybar --trigger aerospace_workspace_change
sketchybar --trigger spotify_change
